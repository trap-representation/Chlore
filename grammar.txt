[Notation]:

A colon (:) following a non-terminal introduces its definition. Terminals are enclosed in backticks (`). Alternative definitions are listed on separate lines, except when prefaced by the words "one of". An optional symbol is indicated by the suffix " (opt)".

----

[Lexical grammar]:

token:
  keyword
  punctuator
  identifier
  constant

keyword: one of
  `pushc` `pushs` `pushi` `pushp` `pushl` `pushuc` `pushus` `pushui`
  `jmp` `jeq` `jgt` `jls` `jge` `jle` `jne` `jz` `jnz`
  `addc` `adds` `addi` `addp` `addbp` `addl` `adduc` `addus` `addui`
  `subc` `subs` `subi` `subp` `subbp` `subl` `subuc` `subus` `subui`
  `mulc` `muls` `muli` `mull` `muluc` `mulus` `mului`
  `divc` `divs` `divi` `divl` `divuc` `divus` `divui`
  `remc` `rems` `remi` `reml` `remuc` `remus` `remui`
  `lshc` `lshs` `lshi` `lshl` `lshuc` `lshus` `lshui`
  `rshc` `rshs` `rshi` `rshl` `rshuc` `rshus` `rshui`
  `orc` `ors` `ori` `orl` `oruc` `orus` `orui`
  `andc` `ands` `andi` `andl` `anduc` `andus` `andui`
  `notc` `nots` `noti` `notl` `notuc` `notus` `notui`
  `xorc` `xors` `xori` `xorl` `xoruc` `xorus` `xorui`
  `swap` `pop` `dup` `ret` `over` `panic`
  `str0` `str1` `str2` `str3` `str4` `str5` `str6` `str7`
  `ldr0` `ldr1` `ldr2` `ldr3` `ldr4` `ldr5` `ldr6` `ldr7`
  `ldhc` `ldhs` `ldhi` `ldhp` `ldhl` `ldhuc` `ldhus` `ldhui`
  `sthc` `sths` `sthi` `sthp` `sthl` `sthuc` `sthus` `sthui`
  `ldcc` `ldcs` `ldci` `ldcp` `ldcl` `ldcuc` `ldcus` `ldcui`
  `hlt` `call` `copy` `pcopy` `popa` `sts` `rbs` `rbe` `pushsp`
  `set` `include` `hltr` `nop`
  `aldhc` `aldhs` `aldhi` `aldhp` `aldhl` `aldhuc` `aldhus` `aldhui`
  `asthc` `asths` `asthi` `asthp` `asthl` `asthuc` `asthus` `asthui`
  `aldcc` `aldcs` `aldci` `aldcp` `aldcl` `aldcuc` `aldcus` `aldcui`
  `alignc` `aligns` `aligni` `alignp` `alignl` `alignuc` `alignus` `alignui`
  `incsp` `decsp` `exit`
  `force_panic` `jmp_panic`
  `pushlt` `import`
  `push0` `push1` `push2` `push3` `push4` `push5` `push7` `pushn1`
  `vstr0` `vstr1` `vstr2` `vstr3` `vstr4` `vstr5` `vstr6` `vstr7`
  `vldr0` `vldr1` `vldr2` `vldr3` `vldr4` `vldr5` `vldr6` `vldr7`
  `vldhc` `vldhs` `vldhi` `vldhp` `vldhl` `vldhuc` `vldhus` `vldhui`
  `vsthc` `vsths` `vsthi` `vsthp` `vsthl` `vsthuc` `vsthus` `vsthui`
  `pushpc` `pushcs`
  `hidden` `exposed` `struct`
  `uc` `us` `ui` `c` `s` `i` `l` `p`
  `alignof` `sizeof`

type:
  `uc` `us` `ui` `c` `s` `i` `l` `p`

decimal-digit: one of
  `0` `1` `2` `3` `4` `5` `6` `7` `8` `9`

decimal-constant:
  decimal-digit
  decimal-digit decimal-constant

octal-digit: one of
  `0` `1` `2` `3` `4` `5` `6` `7`

octal-digit-sequence:
  octal-digit
  octal-digit octal-digit-sequence

octal-constant:
  `0o` octal-digit-sequence

hexadecimal-digit: one of
  `0` `1` `2` `3` `4` `5` `6` `7` `8` `9`
  `a` `b` `c` `d` `e` `f` `A` `B` `C` `D`
  `E` `F`

hexadecimal-digit-sequence:
  hexadecimal-digit
  hexadecimal-digit hexadecimal-digit-sequence

hexadecimal-constant:
  `0x` hexadecimal-digit-sequence

binary-digit: one of
  `0` `1`

binary-digit-sequence:
  binary-digit
  binary-digit binary-digit-sequence

binary-constant:
  `0b` binary-digit-sequence

numeric-constant:
  decimal-conanstant
  octal-constant
  binary-constant
  hexadecimal-constant

constant:
  numeric-constant
  string-constant
  char-constant

char-constant:
  `'` character-sequence-char (opt) `'`

character-sequence-char:
  character-char
  character-char character-sequence-char

character-char:
  any member of the source character set except the `'`, the `\` character, and the newline character
  escape-sequence

string-constant:
  `"` character-sequence-string (opt) `"`

character-sequence-string:
  character-string
  character-string character-sequence-string

character-string:
  any member of the source character set except the `"`, the `\` character, and the newline character
  escape-sequence

escape-sequence:
  `\` escape-character

escape-character:
  any member of the source character set

identifier:
  identifier-nondigit character-sequence-identifier (opt)

character-sequence-identifier:
  character-identifier
  character-identifier character-sequence-identifier

identifier-nondigit: one of
  `a` `b` `c` `d` `e` `f` `g` `h` `i` `j` `k`
  `l` `m` `n` `o` `p` `q` `r` `s` `t` `u` `v`
  `w` `x` `y` `z` `A` `B` `C` `D` `E` `F` `G`
  `H` `I` `J` `K` `L` `M` `N` `O` `P` `Q` `R`
  `S` `T` `U` `V` `W` `X` `Y` `Z` `_` `^` `@`
  `|`   `\` `[` `]` `(` `)` `,` `#` `{` `}`
  `&` `%` `~` `*` the backtick character

character-identifier: one of
  `a` `b` `c` `d` `e` `f` `g` `h` `i` `j` `k`
  `l` `m` `n` `o` `p` `q` `r` `s` `t` `u` `v`
  `w` `x` `y` `z` `A` `B` `C` `D` `E` `F` `G`
  `H` `I` `J` `K` `L` `M` `N` `O` `P` `Q` `R`
  `S` `T` `U` `V` `W` `X` `Y` `Z` `0` `1` `2`
  `3` `4` `5` `6` `7` `8` `9` `_` `^` `@` `|`
  `\` `[` `]` `(` `)` `,` `#` `{` `}` `&` `%`
  `~` `*` the backtick character

punctuator:
  `-` `+` `$` `.` `!`

sign: one of
  `+` `-`

visibility:
  `hidden`
  `exposed`

instruction: one of
  `jmp` `jeq` `jgt` `jls` `jge` `jle` `jne` `jz` `jnz`
  `addc` `adds` `addi` `addp` `addbp` `addl` `adduc` `addus` `addui`
  `subc` `subs` `subi` `subp` `subbp` `subl` `subuc` `subus` `subui`
  `mulc` `muls` `muli` `mull` `muluc` `mulus` `mului`
  `divc` `divs` `divi` `divl` `divuc` `divus` `divui`
  `remc` `rems` `remi` `reml` `remuc` `remus` `remui`
  `lshc` `lshs` `lshi` `lshl` `lshuc` `lshus` `lshui`
  `rshc` `rshs` `rshi` `rshl` `rshuc` `rshus` `rshui`
  `orc` `ors` `ori` `orl` `oruc` `orus` `orui`
  `andc` `ands` `andi` `andl` `anduc` `andus` `andui`
  `notc` `nots` `noti` `notl` `notuc` `notus` `notui`
  `xorc` `xors` `xori` `xorl` `xoruc` `xorus` `xorui`
  `swap` `pop` `dup` `ret` `over` `panic`
  `str0` `str1` `str2` `str3` `str4` `str5` `str6` `str7`
  `ldr0` `ldr1` `ldr2` `ldr3` `ldr4` `ldr5` `ldr6` `ldr7`
  `ldhc` `ldhs` `ldhi` `ldhp` `ldhl` `ldhuc` `ldhus` `ldhui`
  `sthc` `sths` `sthi` `sthp` `sthl` `sthuc` `sthus` `sthui`
  `ldcc` `ldcs` `ldci` `ldcp` `ldcl` `ldcuc` `ldcus` `ldcui`
  `hlt` `call` `copy` `pcopy` `popa` `sts` `rbs` `rbe` `pushsp`
  `set` `include` `hltr` `nop`
  `aldhc` `aldhs` `aldhi` `aldhp` `aldhl` `aldhuc` `aldhus` `aldhui`
  `asthc` `asths` `asthi` `asthp` `asthl` `asthuc` `asthus` `asthui`
  `aldcc` `aldcs` `aldci` `aldcp` `aldcl` `aldcuc` `aldcus` `aldcui`
  `alignc` `aligns` `aligni` `alignp` `alignl` `alignuc` `alignus` `alignui`
  `incsp` `decsp` `exit`
  `force_panic` `jmp_panic`
  `pushlt` `import`
  `push0` `push1` `push2` `push3` `push4` `push5` `push7` `pushn1`
  `vstr0` `vstr1` `vstr2` `vstr3` `vstr4` `vstr5` `vstr6` `vstr7`
  `vldr0` `vldr1` `vldr2` `vldr3` `vldr4` `vldr5` `vldr6` `vldr7`
  `vldhc` `vldhs` `vldhi` `vldhp` `vldhl` `vldhuc` `vldhus` `vldhui`
  `vsthc` `vsths` `vsthi` `vsthp` `vsthl` `vsthuc` `vsthus` `vsthui`
  `pushpc` `pushcs`

push-instruction: one of
  `pushc` `pushs` `pushi` `pushp` `pushl` `pushuc` `pushus` `pushui`

[Phrase structure grammar]:

translation-unit:
  statement-sequence (opt)

statement-sequence:
  statement
  statement statement-sequence

statement:
  keyword
  push-statement
  alignof-statement
  sizeof-statement
  set-statement
  labeled-statement
  import-statement
  struct-definition
  tagged-expression
  instruction

alignof-statement:
  `alignof` type
  `alignof` struct-name

sizeof-statement:
  `sizeof` type
  `sizeof` struct-name

push-statement:
  push-instruction sign (opt) constant
  push-instruction sign (opt) symbol

set-statement:
  `set` identifier sign (opt) constant
  `set` identifier sign (opt) identifier

labeled-statement:
  `:` visibility (opt) symbol

import-statement:
  `import` string-constant

include-statement:
  `include` string-constant

tag:
  identifier

symbol:
  identifier

struct-name:
  identifier

struct-tag-definition:
  type `:` tag
  struct-name `:` tag

struct-tag-definition-sequence:
  struct-tag-definition
  struct-tag-definition struct-tag-definition-sequence

struct-definition:
  `struct` struct-name struct-tag-definition-sequence (opt) `!`

tag-sequence:
  `.` tag
  `.` tag tag-sequence

tagged-expression:
  `+` struct-name tag-sequence (opt)

